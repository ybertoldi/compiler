<FUNC_DEF>      ::= <FUNC_DEC>  OPBRACKET  <STMTS> CLBRACKET;

<FUNC_DEC> ::= <TIPO>  NAME  OPPAREN <FUNC_DEC_ARGS>  CLPAREN
                  | <TIPO>  NAME OPPAREN CLPAREN;

<FUNC_DEC_ARGS> ::= <FUNC_DEC_ARGS> COMMA <TIPO> NAME 
                  | <TIPO> NAME;

<STMTS> ::= <STMTS> <STMT> | <STMT>;
<STMT>  ::= <DEC> 
          | <EXPR>  SMCOLON
          | <ASSIGN>
          | <IF>
          | <IF_ELSE>
          | <WHILE>;

<WHILE> ::=  KW_WHILE  OPPAREN <EXPR>  CLPAREN
             OPBRACKET <STMTS>  CLBRACKET;

<IF>    ::=  KW_IF  OPPAREN <EXPR>  CLPAREN  OPBRACKET <STMTS>  CLBRACKET;

<IF_ELSE> ::=  <IF> <ELSE>;

<ELSE>   ::=  KW_ELSE  OPBRACKET <STMTS>  CLBRACKET
           |  KW_ELSE <IF>;

<DEC>    ::= <TIPO>  NAME  ATTRIBUTION <EXPR>  SMCOLON;
<ASSIGN> ::=  NAME ATTRIBUTION <EXPR>  SMCOLON;



<FUNC_CALL>      ::=  NAME OPPAREN <FUNC_CALL_ARGS>  CLPAREN;
<FUNC_CALL_ARGS> ::= <FUNC_CALL_ARGS>  COMMA <EXPR> | <EXPR>;

<EXPR>   ::= <LOG_OR>
           | <EXPR1>;
<LOG_OR> ::= <EXPR>  LOGIC_OR <EXPR1>;
<EXPR1>   ::= <LOG_AND>
            | <EXPR2>;
<LOG_AND> ::= <EXPR1>  LOGIC_AND <EXPR2>;

<EXPR2> ::= <CMP_EQ>
          | <CMP_NEQ>
          | <EXPR3>;

<CMP_EQ>  ::= <EXPR2>  EQUALS   <EXPR3>;
<CMP_NEQ> ::= <EXPR2>  NEQUALS  <EXPR3>;

<EXPR3>  ::= <CMP_LT>
           | <CMP_LEQT>
           | <CMP_GT>
           | <CMP_GEQT>
           | <EXPR4>;

<CMP_LT>   ::= <EXPR3>  LT    <EXPR4>;
<CMP_LEQT> ::= <EXPR3>  LEQT  <EXPR4>;
<CMP_GT>   ::= <EXPR3>  GT    <EXPR4>;
<CMP_GEQT> ::= <EXPR3>  GEQT  <EXPR4>;

<EXPR4> ::= <SUM>
          | <SUB>
          | <EXPR5>;

<SUM> ::= <EXPR4>  SUM  <EXPR5>;
<SUB> ::= <EXPR4>  SUB  <EXPR5>;

<EXPR5> ::= <MULT>
          | <DIV>
          | <EXPR6>;

<MULT> ::= <EXPR5>  MULT  <EXPR6>;
<DIV>  ::=  <EXPR5>  DIV   <EXPR6>;


<EXPR6> ::=  NAME 
          | <CONST> 
          | <PAREN_EXPR>
          | <FUNC_CALL>;

<PAREN_EXPR> ::=  OPPAREN <EXPR>  CLPAREN;



<CONST> ::=  INT 
          |  FLOAT 
          |  HEXINT 
          |  OCTALINT 
          |  STRLIT
          |  KW_FALSE 
          |  KW_TRUE;

<TIPO>  ::=  KW_INT 
          |  KW_CHAR 
          |  KW_BOOL 
          |  KW_LONG 
          |  KW_FLOAT 
          |  KW_DOUBLE
          |  KW_VOID;
